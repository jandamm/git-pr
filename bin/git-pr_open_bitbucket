#!/usr/bin/env zsh

set -euo pipefail

readonly remote_url="$1"

function create_pr() {
	curl -sw '%{stderr}%{http_code}' \
		-X 'POST' \
		"https://bitbucket.org/api/2.0/repositories/$repo/pullrequests" \
		--netrc \
		-H 'Content-Type: application/json' \
		-d "{
			\"title\": \"$title\",
			\"description\": \"$desc\",
			\"close_source_branch\": true,
			$reviewer
			\"source\": { \"branch\": { \"name\": \"$src\" } },
			\"destination\": { \"branch\": { \"name\": \"$dest\" } }
		}"
}

readonly repo=${${remote_url#*bitbucket.org?}%.git}
readonly title=$(git-pr_get_title)
readonly desc=$(git-pr_get_desc)
readonly dest=$(git-pr_default_branch)
readonly src=$(git branch --show-current)

reviewer=$(git-pr_get_reviewer)
if [[ -n $reviewer ]]
then
	reviewer='"reviewers": [{"username": "'$reviewer'"}],'
fi

tmp=$(mktemp)
readonly response=$(create_pr 2>$tmp)
readonly stat=$(command cat $tmp)
rm $tmp

if [[ $stat -ge 200 ]] && [[ $stat -le 299 ]]
then
	if hash jq &>/dev/null
	then
		echo "Created pr. You can find it here: $(jq .links.html.href <<< $response)"
	else
		echo 'Created pr at bitbucket. (Install jq for more information)'
	fi
	exit 0
else
	echo "Could not create pr ($stat)." >&2
	if hash jq &>/dev/null
	then
		echo "An error occurred: $(jq .error.message <<< $response)" >&2
	else
		echo "Install jq for the error message." >&2
	fi
	exit 1
fi
